plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'application'
}

group 'ru.hse.se.team9'
version '1.0-SNAPSHOT'

def arrow_version = "0.10.4"

allprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://dl.bintray.com/arrow-kt/arrow-kt/" }
    }

    apply plugin: 'org.jetbrains.kotlin.jvm'

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "io.arrow-kt:arrow-core:$arrow_version"
        implementation "io.arrow-kt:arrow-syntax:$arrow_version"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "11"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "11"
    }

    test {
        testLogging {
            exceptionFormat "full"
            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    println "Result: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                }
            }
        }
    }
}

application {
    mainClassName = "ru.hse.se.team9.MainKt"
}

dependencies {
    implementation project(':core')
    implementation project(':model')
    implementation project(':console-view')
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.3.61'
}
